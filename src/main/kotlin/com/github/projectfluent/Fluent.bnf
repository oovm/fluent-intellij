{
    parserClass = "com.github.projectfluent.language.parser.FluentParser"
    parserUtilClass = "com.github.projectfluent.language.psi.FluentParserExtension"

    extends = "com.github.projectfluent.language.psi.FluentElement"

    psiClassPrefix = "Fluent"
    psiImplClassSuffix = "Node"
    psiPackage = "com.github.projectfluent.language.psi"
    psiImplPackage = "com.github.projectfluent.language.psi_node"

    elementTypeHolderClass = "com.github.projectfluent.language.psi.FluentTypes"
    elementTypeClass = "com.github.projectfluent.language.psi.FluentElementType"
    tokenTypeClass = "com.github.projectfluent.language.psi.FluentTokenType"

    psiImplUtilClass = "com.github.projectfluent.language.psi_node.FluentPsiExtension"

    tokens = [
        // comment
        COMMENT_DOCUMENT = "Comment Document"
        COMMENT = "Comment"
        COMMENT_BLOCK = "Comment Block"

        // literal
        URL = "Url"
        SYMBOL = "Symbol"
        STRING_QUOTE = "String Quote"
        STRING_CHAR = "String Character"
        STRING_ESCAPE = "String Escaped"

        PARENTHESIS_L = '('
        PARENTHESIS_R = ')'
        BRACKET_L = '['
        BRACKET_R = ']'
        BRACE_L = '{'
        BRACE_R = '}'
        ANGLE_L = '<'
        ANGLE_R = '>'
        ACCENT = '^'
        EQ = '='
        COLON = ':'
        SEMICOLON = ';'
        COMMA = ','
        DOLLAR = '$'
        DOT = '.'
        STAR = '*'
        AT = '@'
        TO = "->"
        HYPHEN = "-"
//      SPACE='regexp:\s+'
//      ANY = 'regexp:.'
    ]
}

// https://github.com/projectfluent/fluent/blob/master/spec/fluent.ebnf
Fluent ::= Entry*
/* Entries are the main building blocks of Fluent. They define translations and
 * contextual and semantic information about the translations. During the AST
 * construction, adjacent comment lines of the same comment type (defined by
 * the number of #) are joined together. Single-# comments directly preceding
 * Messages and Terms are attached to the Message or Term and are not
 * standalone Entries. */
/* Entries are the main building blocks of Fluent. They define translations and
 * contextual and semantic information about the translations. During the AST
 * construction, adjacent comment lines of the same comment type (defined by
 * the number of #) are joined together. Single-# comments directly preceding
 * Messages and Terms are attached to the Message or Term and are not
 * standalone Entries. */
private Entry ::=
    Message
    | Term
    | COMMENT_LINE
Message ::= MessageID EQ ((Pattern Attribute*) | (Attribute+))
Term    ::= TermID EQ Pattern Attribute*
/* Attributes of Messages and Terms. */
Attribute  ::= AttributeID MessageID EQ Pattern
/* Patterns are values of Messages, Terms, Attributes and Variants. */
Pattern    ::= pattern_element+
/* TextElement and Placeable can occur inline or as block.
 * Text needs to be indented and start with a non-special character.
 * Placeables can start at the beginning of the line or be indented.
 * Adjacent TextElements are joined in AST creation. */
private pattern_element ::=
    TEXT_LINE
    | block_text
    | InlinePlaceable
    | block_placeable
block_text          ::= BLANK_BLOCK BLANK_INLINE INDENT [TEXT_LINE]
InlinePlaceable    ::= BRACE_L (SelectExpression | InlineExpression) BRACE_R
block_placeable     ::= BLANK_BLOCK  InlinePlaceable

/* Rules for validating expressions in Placeables and as selectors of
 * SelectExpressions are documented in spec/valid.md and enforced in
 * syntax/abstract.js. */
InlineExpression    ::=
    StringLiteral
    | NumberLiteral
    | FunctionReference
    | MessageReference
    | TermReference
    | VariableID
    | InlinePlaceable

/* Inline Expressions */
FunctionReference   ::= FunctionID CallArguments
MessageReference    ::= MessageID [AttributeID]
TermReference       ::= TermID [CallArguments]
CallArguments       ::= PARENTHESIS_L (Argument COMMA)* [Argument] PARENTHESIS_R
Argument            ::= NamedArgument | InlineExpression
NamedArgument       ::= MessageID COLON (StringLiteral | NumberLiteral)

/* Block Expressions */
SelectExpression    ::= InlineExpression TO variant_list
variant_list        ::= Variant* DefaultVariant Variant* 
Variant             ::= VariantKey Pattern
DefaultVariant      ::= STAR VariantKey Pattern
VariantKey          ::= BRACKET_L (NumberLiteral | MessageID) BRACKET_R

/* Identifier */
MessageID           ::= SYMBOL;
TermID              ::= HYPHEN SYMBOL;
AttributeID         ::= DOT SYMBOL;
VariableID          ::= DOLLAR SYMBOL;
FunctionID          ::= SYMBOL;
/* Literals */
StringLiteral       ::= STRING_QUOTE STRING_ESCAPE STRING_CHAR STRING_QUOTE
NumberLiteral       ::= INTEGER|DECIMAL